;;;; prelude.bn -- Basic definitions of Bone Lisp.   -*- lisp -*-
;;;; Copyright (C) 2016 Wolfgang Jaehrling
;;;;
;;;; Permission to use, copy, modify, and/or distribute this software for any
;;;; purpose with or without fee is hereby granted, provided that the above
;;;; copyright notice and this permission notice appear in all copies.
;;;;
;;;; THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
;;;; WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
;;;; MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
;;;; ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
;;;; WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
;;;; ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
;;;; OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

;; Need this first because quasiquote expands to it
(_bind 'cat _full-cat)
(_mac-bind 'cat
  (lambda args
    (cons (if (_fast=? 2 (len args)) '_fast-cat 'full-cat) args)))

;; FIXME: defmac has no docstring...
(_mac-bind 'defmac
  (lambda (spec doc . body)
    `(_mac-bind ',(car spec)
       (lambda ,(cdr spec) ,@body))))

(defmac (internmac spec . body)
  "Define an internal macro."
  `(_mac-bind ',(car spec)
     (lambda ,(cdr spec) ,@body)))

(defmac (defsub spec doc . body)
  "Define a sub with name/args as in `spec`, docstring `doc` and code `body`."
  `(_bind ',(car spec)
     (lambda ,(cdr spec) ,@body)))

(internmac (_optimize name binary n-ary)
  `(internmac (,name . args)
     (cons (if (_fast=? 2 (len args)) ',binary ',n-ary) args)))

(_optimize + _fast+ _full+)
(_optimize - _fast- _full-)
(_optimize * _fast* _full*)
(_optimize / _fast/ _full/)
(_optimize =? _fast=? _full=?)
(_optimize >? _fast>? _full>?)
(_optimize <? _fast<? _full<?)
(_optimize >=? _fast>=? _full>=?)
(_optimize <=? _fast<=? _full<=?)
;(_optimize cat _fast-cat _full-cat) ; did this earlier
(_optimize append _fast-cat _full-cat)
(_optimize list+ _fast-cat _full-cat)

(defsub (caar x) "The `car` of the `car` of `x`." (car (car x)))
(defsub (cadr x) "The `car` of the `cdr` of `x`." (car (cdr x)))
(defsub (cdar x) "The `cdr` of the `car` of `x`." (cdr (car x)))
(defsub (cddr x) "The `cdr` of the `cdr` of `x`." (cdr (cdr x)))
(defsub (caaar x) "The `car` of the `car` of the `car` of `x`." (caar (car x)))
(defsub (caadr x) "The `car` of the `car` of the `cdr` of `x`." (caar (cdr x)))
(defsub (cadar x) "The `car` of the `cdr` of the `car` of `x`." (cadr (car x)))
(defsub (caddr x) "The `car` of the `cdr` of the `cdr` of `x`." (cadr (cdr x)))
(defsub (cdaar x) "The `cdr` of the `car` of the `car` of `x`." (cdar (car x)))
(defsub (cdadr x) "The `cdr` of the `car` of the `cdr` of `x`." (cdar (cdr x)))
(defsub (cddar x) "The `cdr` of the `cdr` of the `car` of `x`." (cddr (car x)))
(defsub (cdddr x) "The `cdr` of the `cdr` of the `cdr` of `x`." (cddr (cdr x)))

(defmac (cond . clauses)
  "Shortcut for nested `if` clauses.

Each clause in `clauses` is of the form `(condition . body)`.  The
clauses are processed in order.  The condition of a clause is
evaluated and if it gives a true value, all the expressions in the
body are evaluated; no further clauses will be processed in this case.
If the condition was false, however, the next clause will be
processedin the same way.  If no condition was true, `#f` will be
returned.

It is common to have an else-clause in the end which has `#t` as its
condition."
  (if (nil? clauses)
      #f
    (if (eq? #t (caar clauses))
	`(do ,@(cdar clauses))
      `(if ,(caar clauses)
	   (do ,@(cdar clauses))
	 (cond ,@(cdr clauses))))))

(defmac (and . args)
  "Logical short-circuit AND.

The `args` are evaluated in order until one yields `#f`, in which case
`#f` will also be returned. Otherwise, the result of the last argument
will be returned."
  (cond ((nil? args) #t)
	((nil? (cdr args)) (car args))
	(#t `(if ,(car args) (and ,@(cdr args)) #f))))

(defmac (or . args)
  "Logical short-circuit OR.

The `args` are evaluated in order until one yields a true value, in
which case it will also be returned.  Otherwise, `#f` will be
returned."
  (cond ((nil? args) #f)
	((nil? (cdr args)) (car args))
	(#t `(if ,(car args) (do) (or ,@(cdr args))))))

(defmac (when expr . body)
  "When `expr` is true, evaluate all expressions in `body`."
  `(if ,expr (do ,@body) #f))

(defmac (let bindings . body)
  "let"
  (if (nil? bindings)
      (cons 'do body)
    `(with ,(caar bindings) ,(cadar bindings) (let ,(cdr bindings) . ,body))))

(defmac (in-reg . body)
  "Evaluate `body` while using a new memory region; copy back the result."
  `(_in-reg (lambda () ,@body)))

(defsub (xcons d a)
  "Like `cons`, but order of arguments is reversed.

This can be useful for passing to higher order functions."
  (cons a d))

(defsub (single? x)
  "Check whether `x` is a list of exactly one element.

`x` may be any object, it does not have to be a list."
  (and (cons? x) (nil? (cdr x))))

(defsub (acons key value alist)
  "Put `(key value)` in front of the `alist`."
  (cons (list key value) alist))

(defsub (++ n)
  "Increment `n` by one.  Short for `(+ n 1)`.

The name `++` is inspired by the operator of the same name in the C
programming language, but while the C operator mutates its operand,
the `++` sub obviously does not.  The same is true for `--`.

The traditional Lisp name for this operation was `1+`, which is mostly
fine, but the negative counter part `1-` was a very odd name (inverse
infix?) and thus was rejected in favor of the C notation."
  (+ n 1))

(defsub (-- n)
  "Decrement `n` by one.  Short for `(- n 1)`.

See `++` for background information."
  (- n 1))

(defsub (str-len s)
  "The number of characters in the str `s`."
  (len (unstr s)))


;;;; Aliases and optimizations

(defmac (alias new old)
  "Define `new` as an alias for sub `old`."
  `(_bind ',new ,old))

(alias + _full+)
(alias no nil?)
(alias list->str str)
(alias str->list unstr)
(alias length len)
(alias size len)
(alias str->sym intern)
(alias - _full-)
(alias * _full*)
(alias / _full/)
(alias cons* list*)
(alias contains? member?)
(alias modulo mod)
(alias % mod)
(alias =? _full=?)
(alias >? _full>?)
(alias <? _full<?)
(alias >=? _full>=?)
(alias <=? _full<=?)
(alias macroexpand-1 mac-expand-1)
(alias macroexpand mac-expand)
(alias append cat)
(alias list+ cat)

